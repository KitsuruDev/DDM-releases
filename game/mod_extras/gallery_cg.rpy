## gallery_cg.rpy

default persistent.gallery_lock_imgs = {}

init -1 python in GalleryCG:
    from store import persistent, config, Transform, Hide
    from io import BytesIO
    import os, pygame

    dict, current = {}, None

    def unlock_all():
        for name in persistent.gallery_lock_imgs:
            persistent.gallery_lock_imgs[name]["unlocked"] = True
            dict[name].unlocked = True

    def next(back=False):
        global current, dict
        keys_unlocked = [k for k, v in dict.items() if v.unlocked] # –≤—Å–µ–≥–¥–∞, –∏–Ω–∞—á–µ: –∑–∞–ø–æ–ª–Ω–µ–Ω–∏–µ -> —Ñ–∏–∫—Å–∞—Ü–∏—è -> –æ—Ç–∫—Ä—ã—Ç–∏–µ –Ω–æ–≤–æ–π CG -> –æ–Ω–∞ –Ω–µ –≤ —Å–ø–∏—Å–∫–µ
        index_current = keys_unlocked.index(current)
        index_next = index_current - 1 if back else index_current + 1
        try:               current = keys_unlocked[index_next]
        except IndexError: current = keys_unlocked[0]

    class GalleryCGImage:
        def __init__(self, name, timeline, place, description, artist, image_show, image_export):
            self.name = name
            self.place = place
            self.description = description
            self.artist = artist
            self.timeline = timeline
            self.image_show = Transform(image_show, size=(config.screen_width, config.screen_height))
            self.small_size = Transform(image_show, size=(400, 225), crop=(0, 0, 1280, 720))
            self.image_export = image_export

            if self.name not in persistent.gallery_lock_imgs:
                persistent.gallery_lock_imgs[self.name] = { "unlocked": False }

            self.unlocked = persistent.gallery_lock_imgs[self.name]["unlocked"]
            dict[self.name] = self

        def unlock(self):
            self.unlocked = True
            persistent.gallery_lock_imgs[self.name]["unlocked"] = True

        def export(self):
            if self.image_export:
                if not os.path.exists(config.basedir + "/export_gallery_cg"):
                    try: os.mkdir(config.basedir + "/export_gallery_cg")
                    except: pass

                try:
                    output_surface = pygame.Surface((1280, 720), pygame.SRCALPHA) # SRCALPHA –≤–∫–ª—é—á–∞–µ—Ç –ø–æ–¥–¥–µ—Ä–∂–∫—É –ø—Ä–æ–∑—Ä–∞—á–Ω–æ—Å—Ç–∏

                    if isinstance(self.image_export, tuple):
                        for img_path in self.image_export:
                            f = renpy.file(img_path)
                            image_data = f.read()
                            f.close()
                            output_surface.blit(pygame.image.load(BytesIO(image_data)), (0, 0))
                    else:
                        f = renpy.file(self.image_export)
                        image_data = f.read()
                        f.close()
                        output_surface.blit(pygame.image.load(BytesIO(image_data)), (0, 0))

                    pygame.image.save(output_surface, os.path.join(config.basedir, "export_gallery_cg", self.name + ".png").replace("\\", "/"))

                    message_var = _("–°—Ü–µ–Ω–∞ (CG) —É—Å–ø–µ—à–Ω–æ —ç–∫—Å–ø–æ—Ä—Ç–∏—Ä–æ–≤–∞–Ω–∞ –≤ –ø–∞–ø–∫—É \"export_gallery_cg\".")
                    chibi_var = ("s_chibi hop", 117)

                except Exception as e:
                    message_var = _(f"–ù–µ —É–¥–∞–ª–æ—Å—å —ç–∫—Å–ø–æ—Ä—Ç–∏—Ä–æ–≤–∞—Ç—å —Å—Ü–µ–Ω—É (CG) –∏–∑-–∑–∞ –æ—à–∏–±–∫–∏: {e}")
                    chibi_var = ("s_chibi turned mt ce", 117)

            else:
                message_var = _(f"–ù–µ —É–¥–∞–ª–æ—Å—å —ç–∫—Å–ø–æ—Ä—Ç–∏—Ä–æ–≤–∞—Ç—å —Å—Ü–µ–Ω—É (CG) –∏–∑-–∑–∞ —Å–ª–æ–∂–Ω—ã—Ö —ç—Ñ—Ñ–µ–∫—Ç–æ–≤ –∏\n–ø–æ–∑–∏—Ü–∏–æ–Ω–Ω—ã—Ö –ø–∞—Ä–∞–º–µ—Ç—Ä–æ–≤ —Å–ø—Ä–∞–π—Ç–æ–≤.")
                chibi_var = ("s_chibi turned mt ce", 100)

            renpy.show_screen(
                "extra_screen_help",
                message = message_var,
                ok_action = Hide("extra_screen_help"),
                chibi = chibi_var[0],
                chibi_pos = chibi_var[1]
            )


init python:
    cg_timeline_act_1 = _("–ê–∫—Ç 1 \"–ù–æ–≤–∞—è –∂–∏–∑–Ω—å\". –î–µ–Ω—å")

    cg_a1_d1_y = GalleryCGImage(
        name = "cg_a1_d1_y",
        timeline = f"{cg_timeline_act_1} 1",
        place = _("–õ–µ—Å—Ç–Ω–∏—Ü–∞ —Å—Ç–∞—Ä–æ–≥–æ –∫–æ—Ä–ø—É—Å–∞ —à–∫–æ–ª—ã"),
        description = _("–Æ—Ä–∏ —á–∏—Ç–∞–µ—Ç ¬´–ü–æ—Ä—Ç—Ä–µ—Ç –ú–∞—Ä–∫–æ–≤–∞¬ª –≤ –ª—é–±–∏–º–æ–º –º–µ—Å—Ç–µ –≤ –∫–æ–º–ø–∞–Ω–∏–∏ –ù–∞—Ü—É–∫–∏."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = Composite((1280, 720), (0, 0), "y_cg2_bg", (0, 0), "y_cg2_base", (0, 0), "y_cg2_nochoc", (0, 0), "y_cg2_details", (0, 0), "y_cg2_dust1", (0, 0), "y_cg2_dust2", (0, 0), "y_cg2_dust3", (0, 0), "y_cg2_dust4"),
        image_export = ("images/cg/y_cg2_bg1.png", "images/cg/y_cg2_base.png", "images/cg/y_cg2_nochoc.png", "images/cg/y_cg2_details.png", "images/cg/y_cg2_dust1.png", "images/cg/y_cg2_dust2.png", "images/cg/y_cg2_dust3.png", "images/cg/y_cg2_dust4.png")
    )

    cg_a1_d2_y = GalleryCGImage(
        name = "cg_a1_d2_y",
        timeline = f"{cg_timeline_act_1} 2",
        place = _("–õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω—ã–π –∫–ª—É–±"),
        description = _("–Æ—Ä–∏ —á–∏—Ç–∞–µ—Ç ¬´–ü–æ—Ä—Ç—Ä–µ—Ç –ú–∞—Ä–∫–æ–≤–∞¬ª –≤–º–µ—Å—Ç–µ —Å –ú–∞–∫—Å–æ–º –≤–æ –≤—Ä–µ–º—è –æ–∂–∏–¥–∞–Ω–∏—è –æ–±–º–µ–Ω–∞ —Å—Ç–∏—Ö–∞–º–∏."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = "y_cg1_base",
        image_export = "images/cg/y_cg1_base.png"
    )

    cg_a1_d4_n_1 = GalleryCGImage(
        name = "cg_a1_d4_n_1",
        timeline = f"{cg_timeline_act_1} 4",
        place = _("–ö–ª–∞–¥–æ–≤–∫–∞ –õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω–æ–≥–æ –∫–ª—É–±–∞"),
        description = _("–ù–∞—Ü—É–∫–∏ —Å—Ç–∞–≤–∏—Ç –ª–∏–º–∏—Ç–∏—Ä–æ–≤–∞–Ω–Ω—É—é –∫–æ—Ä–æ–±–∫—É —Å –º–∞–Ω–≥–æ–π –Ω–∞ –ø–æ–ª–∫—É –ø–æ–¥ –ø—Ä–∏—Å–º–æ—Ç—Ä–æ–º –ú–∞–∫—Å–∞."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato\n–£—Å—Ç–∞–ª–æ–µ –≤—ã—Ä–∞–∂–µ–Ω–∏–µ –ª–∏—Ü–∞ -- Sundeerüç®#6114 (Discord, —Å—Ç–∞—Ä—ã–π –Ω–∏–∫)"),
        image_show = Composite((1280, 720), (0, 0), "n_cg2_bg", (0, 0), "n_cg2_base"),
        image_export = ("images/cg/n_cg2_bg.png", "images/cg/n_cg2_base.png")
    )

    cg_a1_d4_n_2 = GalleryCGImage(
        name = "cg_a1_d4_n_2",
        timeline = f"{cg_timeline_act_1} 4",
        place = _("–õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω—ã–π –∫–ª—É–±"),
        description = _("–ù–∞—Ü—É–∫–∏ —É–≤–ª–µ—á—ë–Ω–Ω–æ —Ä–∞—Å—Å–∫–∞–∑—ã–≤–∞–µ—Ç –ú–∞–∫—Å—É —Å—é–∂–µ—Ç –º–∞–Ω–≥–∏."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = Composite((1280, 720), (0, 0), "n_cg1_bg", (0, 0), "n_cg1_base"),
        image_export = ("images/cg/n_cg1_bg.png", "images/cg/n_cg1_base.png")
    )

    cg_a1_d5_night_s = GalleryCGImage(
        name = "cg_a1_d5_night_s",
        timeline = f"{cg_timeline_act_1} 5",
        place = _("–°–ø–∞–ª—å–Ω—è –°–∞–π–æ—Ä–∏"),
        description = _("–î–∞–≤–Ω–æ –ø–æ–≥–∏–±—à–∏–π –æ—Ç —Å–æ–±—Å—Ç–≤–µ–Ω–Ω—ã—Ö —Ä—É–∫ –æ–±—Ä–∞–∑ –°–∞–π–æ—Ä–∏ –≤ –∫–æ—à–º–∞—Ä–µ –ú–∞–∫—Å–∞."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = Composite((1280, 720), (0, 0), "s_kill_bg_zoom", (0, 0), "s_kill_zoom", (0, 0), "s_kill_zoom_trans", (0, 0), Transform("noise", alpha=0.25), (0, 0), Transform("vignette", alpha=0.75)),
        image_export = None
    )

    cg_a1_d6_s_1 = GalleryCGImage(
        name = "cg_a1_d6_s_1",
        timeline = f"{cg_timeline_act_1} 6",
        place = _("–°–ø–∞–ª—å–Ω—è –≤ –¥–æ–º–µ –°–∞–π–æ—Ä–∏"),
        description = _("–°–∞–π–æ—Ä–∏ –ª–µ–∂–∏—Ç –≤ –∫—Ä–æ–≤–∞—Ç–∏ —Å–ø—É—Å—Ç—è –¥–µ–Ω—å –ø–æ—Å–ª–µ –∫–æ–Ω—Ñ–ª–∏–∫—Ç–∞ –≤ –õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω–æ–º –∫–ª—É–±–µ."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- staticquit#8020 (Discord, —Å—Ç–∞—Ä—ã–π –Ω–∏–∫)"),
        image_show = "sayori_cg_act_1_day_6",
        image_export = "mod_assets/cg/main_history/act_1/day_6/s.png"
    )

    cg_a1_d6_s_2 = GalleryCGImage(
        name = "cg_a1_d6_s_2",
        timeline = f"{cg_timeline_act_1} 6",
        place = _("–ü–µ—Ä–µ–¥ –¥–æ–º–æ–º –°–∞–π–æ—Ä–∏"),
        description = _("–ú–∞–∫—Å –≤—ã—Å–ª—É—à–∏–≤–∞–µ—Ç –°–∞–π–æ—Ä–∏ –∏ —É—Å–ø–æ–∫–∞–∏–≤–∞–µ—Ç –µ—ë –ø–æ—Å–ª–µ –∫–æ–Ω—Ñ–ª–∏–∫—Ç–∞ –≤ –õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω–æ–º –∫–ª—É–±–µ."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = "s_cg3",
        image_export = "images/cg/s_cg3.png" # —Ä–∞–∑–≤–µ—Ä–Ω—É—Ç—å –ø—Ä–∏ —ç—Ç–æ–º –∫–æ–¥–µ –Ω–µ–≤–æ–∑–º–æ–∂–Ω–æ
    )

    cg_a1_d7_n = GalleryCGImage(
        name = "cg_a1_d7_n",
        timeline = f"{cg_timeline_act_1} 7",
        place = _("–ö—É—Ö–Ω—è –≤ –¥–æ–º–µ –ú–∞–∫—Å–∞"),
        description = _("–ú–∞–∫—Å —Å—Ö–≤–∞—Ç–∏–ª –ù–∞—Ü—É–∫–∏ –≤–æ –≤—Ä–µ–º—è –ø—Ä–æ–≤–µ—Ä–∫–∏ –≥–ª–∞–∑—É—Ä–∏ –Ω–∞ –∫–∏—Å–ª–æ—Å—Ç—å."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = Composite((1280, 720), (0, 0), "n_cg3_base", (0, 0), "n_cg3_cake", (0, 0), "n_cg3_exp1"),
        image_export = ("images/cg/n_cg3_base.png", "images/cg/n_cg3_cake.png", "images/cg/n_cg3_exp1.png")
    )

    cg_a1_d7_cup = GalleryCGImage(
        name = "cg_a1_d7_n",
        timeline = f"{cg_timeline_act_1} 7",
        place = _("–ö—É—Ö–Ω—è –≤ –¥–æ–º–µ –ú–∞–∫—Å–∞"),
        description = _("–ò—Å–ø–µ—á—ë–Ω–Ω—ã–µ –∫–µ–∫—Å—ã –¥–ª—è –ø—Ä–∞–∑–¥–Ω–æ–≤–∞–Ω–∏—è –≤–æ—Å—Å–æ–µ–¥–∏–Ω–µ–Ω–∏—è –õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω–æ–≥–æ –∫–ª—É–±–∞."),
        artist = _("—Ä–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ -- KitsuruDev (–∏—Å–ø–æ–ª—å–∑–æ–≤–∞–Ω—ã —Å–≤–æ–±–æ–¥–Ω—ã–µ –∏–∑–æ–±—Ä–∞–∂–µ–Ω–∏—è)"),
        image_show = "cupcakes_cg_act_1_day_7",
        image_export = "mod_assets/cg/main_history/act_1/day_7/cup.png"
    )

    cg_a1_d8_s = GalleryCGImage(
        name = "cg_a1_d8_s",
        timeline = f"{cg_timeline_act_1} 8",
        place = _("–õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω—ã–π –∫–ª—É–±"),
        description = _("–ú–∞–∫—Å –ø–æ–ø—Ä–∞–≤–ª—è–µ—Ç –æ–¥–µ–∂–¥—É –°–∞–π–æ—Ä–∏ –≤–æ –≤—Ä–µ–º—è –æ–∂–∏–¥–∞–Ω–∏—è —á–∞–µ–ø–∏—Ç–∏—è."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato\n–†–∞–¥–æ—Å—Ç–Ω–æ–µ –≤—ã—Ä–∞–∂–µ–Ω–∏–µ –ª–∏—Ü–∞ –∏ –∑–∞–∫—Ä—ã—Ç—ã–µ –≥–ª–∞–∑–∞ -- Reitanna"),
        image_show = Composite((1280, 720), (0, 0), "s_cg1_base", (0, 0), "s_cg1_exp1"),
        image_export = ("images/cg/n_cg3_base.png", "images/cg/n_cg3_cake.png", "images/cg/n_cg3_exp1.png")
    )

    cg_a1_d10_night_n_1 = GalleryCGImage(
        name = "cg_a1_d10_night_n_1",
        timeline = f"{cg_timeline_act_1} 10",
        place = _("–ö–ª–∞–¥–æ–≤–∫–∞ –õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω–æ–≥–æ –∫–ª—É–±–∞"),
        description = _("–û–±—Ä–∞–∑ –ù–∞—Ü—É–∫–∏ —Å—Ç–∞–≤–∏—Ç –∫–æ—Ä–æ–±–∫—É —Å –º–∞–Ω–≥–æ–π –Ω–∞ –ø–æ–ª–∫—É –≤ –∫–æ—à–º–∞—Ä–µ –ú–∞–∫—Å–∞."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato\n–†–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ —Å–ø—Ä–∞–π—Ç–∞ –ø–µ—Ä—Å–æ–Ω–∞–∂–∞ -- SpringingTraps#5243 (Discord, —Å—Ç–∞—Ä—ã–π –Ω–∏–∫)"),
        image_show = Composite((1280, 720), (0, 0), "n_cg2_bg", (0, 0), "n_cg2_base_horror_gallery_cg"),
        image_export = None
    )

    cg_a1_d10_night_n_2 = GalleryCGImage(
        name = "cg_a1_d10_night_n_2",
        timeline = f"{cg_timeline_act_1} 10",
        place = _("–õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω—ã–π –∫–ª—É–±"),
        description = _("–û–±—Ä–∞–∑ –ù–∞—Ü—É–∫–∏ —Ä–∞—Å—Å–∫–∞–∑—ã–≤–∞–µ—Ç –ú–∞–∫—Å—É –æ –Ω–µ–¥–æ—Å—Ç–∞—Ç–∫–µ –≤–Ω–∏–º–∞–Ω–∏—è –≤ –µ–≥–æ –∫–æ—à–º–∞—Ä–µ."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = Composite((1280, 720), (0, 0), "n_cg1_bg", (0, 0), "n_cg1_base", (0, 0), "n_cg1b"),
        image_export = None
    )

    cg_a1_d11_y = GalleryCGImage(
        name = "cg_a1_d11_y",
        timeline = f"{cg_timeline_act_1} 11",
        place = _("–°–ø–∞–ª—å–Ω—è –≤ –¥–æ–º–µ –ú–∞–∫—Å–∞"),
        description = _("–Æ—Ä–∏ –Ω–∞—Å–ª–∞–∂–¥–∞–µ—Ç—Å—è –º—è–≥–∫–∏–º –ø–æ–ª–æ—Ç–µ–Ω—Ü–µ–º, –ø—Ä–∏–¥–µ—Ä–∂–∏–≤–∞—è —Ä—É–∫—É –ú–∞–∫—Å–∞."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = "y_cg3_base",
        image_export = "images/cg/y_cg3_base.png"
    )

    cg_a1_d11_night_mmm = GalleryCGImage(
        name = "cg_a1_d11_night_mmm",
        timeline = f"{cg_timeline_act_1} 11",
        place = _("–ö–æ—Ä–∏–¥–æ—Ä —à–∫–æ–ª—ã"),
        description = _("–ú–æ–Ω–∏–∫–∞–º–º–º –æ—Å–º–∞—Ç—Ä–∏–≤–∞–µ—Ç –ú–∞–∫—Å–∞ –≤–±–ª–∏–∑–∏ –≤ –µ–≥–æ –∫–æ—à–º–∞—Ä–µ."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ –æ–±—Ä–∞–∑–∞ -- Satchely –∏–∑ Team Salvato\n–†–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ —Å–ø—Ä–∞–π—Ç–∞ -- KitsuruDev"),
        image_show = Composite((1280, 720), (0, 0), "bg glitch", (-300, -500), "monikammm_cg_act_1_day_11_gallery_cg"),
        image_export = None
    )

    cg_a1_d12_s = GalleryCGImage(
        name = "cg_a1_d12_s",
        timeline = f"{cg_timeline_act_1} 12",
        place = _("–ö–ª–∞–¥–æ–≤–∫–∞ –õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω–æ–≥–æ –∫–ª—É–±–∞"),
        description = _("–°–∞–π–æ—Ä–∏ –æ—Ö–ª–∞–∂–¥–∞–µ—Ç —Å–≤–æ–π –ª–æ–± –±—É—Ç—ã–ª–∫–æ–π —Å —è–±–ª–æ—á–Ω—ã–º —Å–æ–∫–æ–º –ø–æ—Å–ª–µ —É–¥–∞—Ä–∞ –æ –∫–æ—Å—è–∫."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = "s_cg2_base2",
        image_export = "images/cg/s_cg2_base2.png"
    )

    cg_a1_d12_night_y = GalleryCGImage(
        name = "cg_a1_d12_night_y",
        timeline = f"{cg_timeline_act_1} 12",
        place = _("–õ–∏—Ç–µ—Ä–∞—Ç—É—Ä–Ω—ã–π –∫–ª—É–±"),
        description = _("–ü–æ–≥–∏–±—à–∏–π –æ—Ç —Ä—É–∫ –ú–∞–∫—Å–∞ –æ–±—Ä–∞–∑ –Æ—Ä–∏ –≤ –µ–≥–æ –∫–æ—à–º–∞—Ä–µ."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ –æ–±—Ä–∞–∑–∞ -- Satchely –∏–∑ Team Salvato"),
        image_show = "images/cg/y_kill/1a.png",
        image_export = None
    )

    cg_a1_d13_night_mmm = GalleryCGImage(
        name = "cg_a1_d13_night_mmm",
        timeline = f"{cg_timeline_act_1} 13",
        place = _("–ö–ª–∞—Å—Å —à–∫–æ–ª—ã"),
        description = _("–ú–æ–Ω–∏–∫–∞–º–º–º —Ä–∞–∑–≥–æ–≤–∞—Ä–∏–≤–∞–µ—Ç —Å –ú–∞–∫—Å–æ–º –≤ –µ–≥–æ –∫–æ—à–º–∞—Ä–µ."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato\n–†–µ–¥–∞–∫—Ç–∏—Ä–æ–≤–∞–Ω–∏–µ —Å–ø—Ä–∞–π—Ç–∞ -- KitsuruDev"),
        image_show = Composite((1280, 720), (0, 0), "black", (0, 0), "monikammm_cg_act_1_day_13_mask_smoke", (0, 0), "monikammm_cg_act_1_day_13_mask_smoke_flip", (0, 0), "monikammm_cg_act_1_day_13_mask_grain", (0, 0), "monikammm_cg_act_1_day_13_mask_gas_cloud", (0, 0), "monika_room", (0, 0), "monika_room_highlight", (0, 0), "monika_room_desk", (0, 0), "monikammm desk hcross"),
        image_export = None
    )

    cg_a1_d14_m = GalleryCGImage(
        name = "cg_a1_d14_m",
        timeline = f"{cg_timeline_act_1} 14",
        place = _("–ì–æ—Å—Ç–∏–Ω–∞—è –¥–æ–º–∞ –ú–æ–Ω–∏–∫–∏"),
        description = _("–ú–æ–Ω–∏–∫–∞ –≤ –æ–∂–∏–¥–∞–Ω–∏–∏ –ø–æ—Ü–µ–ª—É—è –Ω–∞ —Å–≤–∏–¥–∞–Ω–∏–∏ —Å –ú–∞–∫—Å–æ–º."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫–∏ -- Itchylychi, Cyrke (Reddit), Chiff The Oblivious"),
        image_show = Composite((1280, 720), (0, 0), "monika_cg_act_1_day_14_base", (0, 0), "monika_cg_act_1_day_14_exp3"),
        image_export = ("mod_assets/cg/main_history/act_1/day_14/m_base.png", "mod_assets/cg/main_history/act_1/day_14/m_exp3.png")
    )

    cg_menu_m = GalleryCGImage(
        name = "cg_menu_m",
        timeline = _("–í—Å—ë –≤—Ä–µ–º—è"),
        place = _("–ì–ª–∞–≤–Ω–æ–µ –º–µ–Ω—é"),
        description = _("–ù–∞ —Å—Ç—Ä–∞–∂–µ —Ü–µ–ª–æ—Å—Ç–Ω–æ—Å—Ç–∏ –∏ —Ä–∞–±–æ—Ç–æ—Å–ø–æ—Å–æ–±–Ω–æ—Å—Ç–∏ –º–æ–¥–∏—Ñ–∏–∫–∞—Ü–∏–∏."),
        artist = _("–•—É–¥–æ–∂–Ω–∏–∫ -- Satchely –∏–∑ Team Salvato"),
        image_show = Composite((1280, 720), (0, 0), "menu_bg", (0, 0), "images/cg/monika/monika_bg.png"),
        image_export = None
    )


## Gallery Screen #############################################################


screen gallery_cg():
    tag menu

    style_prefix "gallery_cg"

    use game_menu(_("–ì–∞–ª–µ—Ä–µ—è")):

        fixed:

            vpgrid id "gcgvp":
                rows math.ceil(len(GalleryCG.dict) / 2.0)
                cols 2

                mousewheel True
                arrowkeys True
                allow_underfull True

                spacing 50

                align (0.35, 1.0)
                ysize 600

                for name, gl in GalleryCG.dict.items():
                    vbox:
                        xsize 400 ysize 370

                        if gl.unlocked:
                            label "[gl.timeline]"

                            null height 5

                            imagebutton:
                                idle gl.small_size
                                action [SetVariable("GalleryCG.current", name), ShowMenu("preview_cg"), With(Dissolve(0.5))]

                            null height 10

                            text "[gl.place]" style "gallery_cg_place_text"

                            null height 5

                            text "[gl.description]"

                        else:
                            label "???"

                            null height 5

                            imagebutton:
                                idle "mod_assets/mod_extra_images/gallery_cg/lock.png"
                                action [
                                    Play("sound", gui.activate_sound),
                                    Show(
                                        "extra_screen_help",
                                        message = _("–ü—Ä–æ–¥–æ–ª–∂–∞–π—Ç–µ –ø—Ä–æ—Ö–æ–¥–∏—Ç—å —Å—é–∂–µ—Ç,\n—á—Ç–æ–±—ã –æ—Ç–∫—Ä—ã—Ç—å —ç—Ç—É CG-—Å—Ü–µ–Ω—É."),
                                        ok_action = Hide("extra_screen_help"),
                                        chibi = "s_chibi turned mt ce",
                                        chibi_pos = 95
                                    )
                                ]

                            null height 10

                            text _("CG-—Å—Ü–µ–Ω–∞ –∑–∞–±–ª–æ–∫–∏—Ä–æ–≤–∞–Ω–∞") style "gallery_cg_place_text"

                            null height 5

                            text "???"

            vbar value YScrollValue("gcgvp") xalign 1.012 ypos -0.08 ysize 570


        textbutton "?":
            style "return_button"
            text_size 35
            pos (0.985, 1.1)
            action ShowMenu(
                "extra_screen_help",
                _("–ü–æ–º–æ—â—å\n–î–ª—è –ø—Ä–æ—Å–º–æ—Ç—Ä–∞ —Å—Ü–µ–Ω—ã (CG) –≤–æ –≤–µ—Å—å —ç–∫—Ä–∞–Ω –Ω–∞–∂–º–∏—Ç–µ –Ω–∞ –µ–≥–æ —Ä–∞–∑–±–ª–æ–∫–∏—Ä–æ–≤–∞–Ω–Ω—É—é –º–∏–Ω–∏–∞—Ç—é—Ä—É.\n–ß—Ç–æ–±—ã –ø–µ—Ä–µ–∫–ª—é—á–∞—Ç—å CG –ø—Ä–∏ –ø—Ä–æ—Å–º–æ—Ç—Ä–µ, –Ω–∞–∂–º–∏—Ç–µ –Ω–∞ –±–æ–∫–æ–≤—ã–µ —Å—Ç—Ä–µ–ª–∫–∏.\n–ß—Ç–æ–±—ã –≤—ã–π—Ç–∏ –∏–∑ —Ä–µ–∂–∏–º–∞ –ø—Ä–æ—Å–º–æ—Ç—Ä–∞, –Ω–∞–∂–º–∏—Ç–µ \"X\".\n–ß—Ç–æ–±—ã —ç–∫—Å–ø–æ—Ä—Ç–∏—Ä–æ–≤–∞—Ç—å CG –∫ —Å–µ–±–µ –Ω–∞ –∫–æ–º–ø—å—é—Ç–µ—Ä, –Ω–∞–∂–º–∏—Ç–µ \"E\".\n–ü–∞–ø–∫–∞ —ç—Å–∫–ø–æ—Ä—Ç–∞ –±—É–¥–µ—Ç —É–∫–∞–∑–∞–Ω–∞ –≤–æ –≤—Å–ø–ª—ã–≤–∞—é—â–µ–º –æ–∫–Ω–µ."),
                ok_action = Hide("extra_screen_help"),
                chibi = "y_chibi turned magnifier",
                chibi_pos = 30
            )

        if config.developer:
            textbutton "UN":
                text_size 30
                pos (0.89, 0.98)
                action Function(GalleryCG.unlock_all)


style gallery_cg_text is gui_text
style gallery_cg_place_text is gallery_cg_text
style gallery_cg_label_text is gallery_cg_place_text

style gallery_cg_text:
    font "mod_assets/font/menu/AA_Futured.ttf"
    color "#000"
    outlines []
    size 18
    text_align 0.5
    xalign 0.5

style gallery_cg_place_text:
    color "#fff"
    outlines [(2, "#505050", 0, 0)]
    size 20

style gallery_cg_label:
    xalign 0.5

style gallery_cg_label_text:
    font "mod_assets/font/menu/Vivl-rail.ttf"
    outlines [(2, text_outline_color, 0, 0), (1, text_outline_color, 2, 2)]



## Gallery Screen #################################################################
##
## This screen shows the currently selected screen to the player in-game.
screen preview_cg():

    tag menu

    hbox:
        add GalleryCG.dict[GalleryCG.current].image_show

    hbox:
        xalign 0.999 ypos 0.01
        spacing 7

        textbutton "i":
            text_style "preview_cg_textbutton_text"
            text_size 30
            activate_sound gui.activate_sound
            action Show("dialog", message=GalleryCG.dict[GalleryCG.current].artist, ok_action=Hide("dialog"))

        textbutton "E":
            text_style "preview_cg_textbutton_text"
            text_size 30
            activate_sound gui.activate_sound
            action Function(GalleryCG.dict[GalleryCG.current].export)

        textbutton "X":
            text_style "preview_cg_textbutton_text"
            text_size 30
            activate_sound gui.activate_sound
            action ShowMenu("gallery_cg")

    textbutton "<":
        text_style "preview_cg_textbutton_text"
        xalign 0.0 yalign 0.5
        action Function(GalleryCG.next, True)

    textbutton ">":
        text_style "preview_cg_textbutton_text"
        xalign 1.0 yalign 0.5
        action Function(GalleryCG.next)

    on "replaced" action With(Dissolve(0.5))


style preview_cg_textbutton_text is navigation_button_text
style preview_cg_textbutton_text:
    font "mod_assets/font/menu/UZSans-SemiBold.ttf"
    size 40
